aspectdef RangeValueMonitorValidExprs
	input $function, varName end
	output exprs end
	
	exprs = [];
	
	select $function.body.stmt.expr end
	apply
		// Ignore expressions without name
		if($expr.name === undefined) {
			continue;
		}

		// Ignore expressions if name does not match
		if($expr.name !== varName) {
			continue;
		}

		// Monitor only writes
		if($expr.use === "read") {
			continue;
		}

		// Ignore expressions inside loop headers
		if($expr.isInsideLoopHeader) {
			continue;
		}

		exprs.push($expr);
		
	end

end

aspectdef InitRangeMonitor
	input monitorName, $monitorH end

	select file.function{'main'}.body end
	apply
		$body.exec insertBegin(monitorName + '_range_init();');
		$file.addIncludeJp($monitorH);
	end
	
end