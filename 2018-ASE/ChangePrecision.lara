aspectdef ChangePrecision

	input $func, $oldType, $newType end
	
	/* change type of variable declarations found
	 *  inside the function and parameters */
	select $func.decl end
	apply
		changeJpType($decl, $oldType, $newType);
//		var changedType = changeType($decl.type, $oldType, $newType);
//		def type = changedType;
		
		// If decl has initialization, change types in initialization expression
		if($decl.hasInit) {
			$initExpr = $decl.init;
//			var initChangedType = changeType($initExpr.type, $oldType, $newType);
//			$initExpr.def type = initChangedType;			

			//println("INIT EXPR:" + $initExpr.ast);

			for(var $descendantExpr of $initExpr.descendantsAndSelf("expression")) {
				//println("Descendat: " + $descendantExpr.code);
				//println("Descendat type before:" + $descendantExpr.type.code);
				//var $exprChangedType = changeType($descendantExpr.type, $oldType, $newType);
				//$descendantExpr.def type = $exprChangedType;
				//println("Descendat type after:" + $descendantExpr.type.code);				
				
				changeJpType($descendantExpr, $oldType, $newType);
			}
		}
	end


	
	/* do the same with the function return type ... */
	var $returnType = $func.functionType.returnType;
	var $changedReturnType = changeType($returnType, $oldType, $newType);
	$func.functionType.def returnType = $changedReturnType;
//	if($func.functionType.returnType.equals($oldType))
//		$func.functionType.def returnType = $newType;
	
end

function changeJpType($jp, $oldType, $newType) {

	if($jp.type === undefined) {
		return;
	}

	var changedType = changeType($jp.type, $oldType, $newType);
	$jp.def type = changedType;
	
	// Special case: sizeof
	if($jp.instanceOf("unaryExprOrType") && $jp.kind === "sizeof") {
		if($jp.argType.equals($oldType)) {
			$jp.def argType = $newType;
		}
	}
	
}


function changeType($targetType, $oldType, $newType) {

	/* Replace target type */
	if($targetType.equals($oldType)) {
		return $newType;
	}
	
	/* Get underlying type*/
	var underlyingType = getUnderlyingType($targetType);
	/* If no underlying type, just return type*/
	if(underlyingType === undefined) {
		return $targetType;
	}
	
	/* Apply change to underlying type */
	var changedUnderlying = changeType(underlyingType, $oldType, $newType);
	
	/* Update if there are changes */
	if(!changedUnderlying.equals(underlyingType)) {
		underlyingType.replaceWith(changedUnderlying);
	}
	
	return $targetType;
}


function getUnderlyingType($type) {
	if($type.hasSugar) {
		return $type.desugar;
	}
	
	if($type.isPointer) {
		return $type.pointee;
	}
	
	if($type.isArray) {
		return $type.elementType;
	}
	
	if($type.joinPointType === "qualType") {
		return $type.unqualifiedType;
	}

	return undefined;
}
